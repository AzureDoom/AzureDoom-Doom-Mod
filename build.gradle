buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url  = 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
		classpath "com.wynprice.cursemaven:CurseMaven:2.1.5"
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.wynprice.cursemaven"
apply plugin: 'com.matthewprenger.cursegradle'

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

version = "4.0.0"
group = "mod.azure.doomweapon"
archivesBaseName = "MCDoom-Mod-1.16.5"

java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users, so your mod should target Java 8.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.16.5'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            mods {
                doomweapon {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            mods {
                doomweapon {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            args '--mod', 'doomweapon', '--all', '--output', file('src/generated/resources/')
            mods {
                doomweapon {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
	jcenter()
	maven {
        name = "jitpack"
        url "https://jitpack.io"
    }
    maven { url = "https://maven.theillusivec4.top/" }
    maven {
        url 'https://www.dogforce-games.com/maven/'
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven { url 'https://repo.repsy.io/mvn/gandiber/geckolib' }
    maven { url 'https://maven.blamejared.com' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.2'
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.16.3-4.0.2.0")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.16.3-4.0.2.0:api")
	compile fg.deobf("curse.maven:SD-392762:3069533")
	compile fg.deobf("curse.maven:JEI-238222:3108050")
	//compile fg.deobf("curse.maven:GTE-417802:3114274")
	//compile fg.deobf("curse.maven:GTEP-283644:3092113")
	compileOnly fg.deobf("vazkii.patchouli:Patchouli:1.16.4-50:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.16.4-50")
	compile fg.deobf("curse.maven:TDI-245733:3144151")
	compile fg.deobf("curse.maven:projectmmo-353935:3266512")
	compile fg.deobf("curse.maven:worldedit-225608:3135187")
	implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.16.5:3.0.30')
}

jar {
    manifest {
        attributes([
            "Specification-Title": "doomweapon",
            "Specification-Vendor": "azuredoom",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"azuredoom",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "doom.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar') 

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "${curse_id}"
        	releaseType = "${curse_release}"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
        	addGameVersion("1.16.5")
        	addGameVersion("Forge")
        	addGameVersion("Java 8")

        	relations {
            	requiredDependency 'curios'  
            	requiredDependency 'geckolib' 
            	optionalDependency 'project-mmo'   
            	optionalDependency 'patchouli'     
        	}
    	}
    }
}
