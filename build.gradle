buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url  = 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
		classpath "com.wynprice.cursemaven:CurseMaven:2.1.5"
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.wynprice.cursemaven"
apply plugin: 'com.matthewprenger.cursegradle'

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

version = "3.6.0"
group = "mod.azure.doomweapon"
archivesBaseName = "MCDoom-Mod-1.16.4"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            mods {
                doomweapon {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            mods {
                doomweapon {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            args '--mod', 'doomweapon', '--all', '--output', file('src/generated/resources/')
            mods {
                doomweapon {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
	jcenter()
	maven {
        name = "jitpack"
        url "https://jitpack.io"
    }
    maven { url = "https://maven.theillusivec4.top/" }
    maven {
        url 'https://www.dogforce-games.com/maven/'
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven { url 'https://repo.repsy.io/mvn/gandiber/geckolib' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.4-35.0.12'
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.16.3-4.0.2.0")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.16.3-4.0.2.0:api")
	compile fg.deobf("curse.maven:SD:3069533")
	compile fg.deobf("curse.maven:JEI:3108050")
	compile fg.deobf("curse.maven:GTE:3114274")
	compile fg.deobf("curse.maven:GTEP:3092113")
	compile fg.deobf("curse.maven:projectmmo:3100529")
	implementation fg.deobf('software.bernie.geckolib:forge-1.16.4-geckolib:3.0.0-beta.10')
}

jar {
    manifest {
        attributes([
            "Specification-Title": "doomweapon",
            "Specification-Vendor": "azuredoom",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"azuredoom",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "doom.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar') 

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

curseforge {

    def curseProp = new Properties()
    File secretPropsFile = file("key.properties")
    curseProp.load(secretPropsFile.newInputStream())

    project {
        apiKey = curseProp.getProperty('curseKey')
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'text'
        changelog = file('CHANGELOG.txt')
        addGameVersion("1.16.3")
        addGameVersion("1.16.4")
        addGameVersion("Forge")
        addGameVersion("Java 8")

        relations {
            requiredDependency 'curios'  
            requiredDependency 'geckolib' 
            optionalDependency 'project-mmo'      
        }
    }
}